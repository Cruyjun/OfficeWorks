//===========================================================================
//
//  File Name:    Setup.rul
//
//  Description:  Blank setup main script file
//
//  Comments:     Blank setup is an empty setup project. If you want to
//				  create a new project via. step-by step instructions use the
//				  Project Assistant.
//
//===========================================================================

// Included header files ----------------------------------------------------
#include "ifx.h"

// User Functions
prototype STRING GetConnectionInformation();
prototype ShowSQLErrorMessage(STRING);
prototype ShowFinishDialog(STRING);

// Global Variables
STRING 	svDatabaseName,svDatabasePath;
STRING 	svServerName,svUserName,svUserPwd,svDriver;

// Global Constants
#define kDATABASE_NAME					"OW"
#define kDATABASE_VERSION				"v5.0.0"
// OfficeWorks License file
#define kLICENSE_FILE_NAME				"Licenca_Utilizacao_OfficeWorks.rtf"

                   
//---------------------------------------------------------------------------                                                                        
// OnFirstUIBefore
//
// First Install UI Sequence - Before Move Data
//
// The OnFirstUIBefore event is called by OnShowUI when the setup is
// running in first install mode. By default this event displays UI allowing
// the end user to specify installation parameters.
//
// Note: This event will not be called automatically in a
// program...endprogram style setup.
//---------------------------------------------------------------------------
function OnFirstUIBefore()
    number  nResult;
    string  szTitle,szMsg,szText1,szField1,svEdit1;
    string  szDatabasePathLocal,szDatabasePathNotLocal;
    string  szLicenseFile,szOpt1,szOpt2;
    BOOL	bLicenseAccepted;
begin	
	// Set setup title
	IFX_SETUP_TITLE = "Instalação da Base de Dados do OfficeWorks - InstallShield Wizard";
	// Beginning of UI Sequence
	Dlg_Start:
	    nResult = 0;
	    // Initialize variables
	    svServerName ="";
	    svUserName ="";
	    svUserPwd ="";
	    svDatabaseName = kDATABASE_NAME;
	    svDatabasePath = "";
	    szDatabasePathLocal = "";
	    szDatabasePathNotLocal = "";
		bLicenseAccepted = FALSE;
		szLicenseFile = SUPPORTDIR ^ kLICENSE_FILE_NAME;
	
	// Welcome screen
	Dlg_SdWelcome:
	    szTitle = "Bem-vindo ao assistente de instalação da Base de Dados do OfficeWorks.";
	    szMsg = "O InstallShield Wizard vai instalar a Base de Dados (" + kDATABASE_VERSION + ") do OfficeWorks.\n\nPara continuar faça clique sobre Seguinte.";
	    nResult = SdWelcome(szTitle,szMsg);
	    if(nResult = BACK) goto Dlg_Start;
	
	// License Dialog
	Dlg_SdLicense:
	    szTitle = " ";
	    nResult = SdLicense2Rtf(szTitle,szOpt1,szOpt2,szLicenseFile,bLicenseAccepted);
	    if (nResult = BACK) goto Dlg_SdWelcome;
	
	// Connect to sql server
	Dlg_SQLServer:
	    nResult = OnSQLServerInitialize(nResult);
	    if(nResult = BACK) goto Dlg_SdLicense;
	
    // Get connection info
	GetConnectionInformation();

 	// Get OfficeWorks database name
	Dlg_SQLDatabaseName:
	    szTitle = " ";
    	szMsg = "Indique o nome para a Base de Dados do OfficeWorks\n\n";
		szField1 = "Nome:";
		nResult = SdShowDlgEdit1(szTitle,szMsg,szField1,svDatabaseName);
		if(nResult = BACK) goto Dlg_SQLServer;

	// Validate if database name is filled
	if(svDatabaseName = "") then
		MessageBox("Não é possível continuar sem indicar o nome da Base de Dados!",WARNING);
		goto Dlg_SQLDatabaseName;        
	endif;      
	
	// Get Dir for data files
	Dlg_SQLDatabasePath:
	    szTitle = " ";
	    if(svServerName != "(Local)") then
	    	szMsg = "Indique a pasta de destino para os ficheiros da Base de Dados do OfficeWorks\n\n";
	    	szMsg = szMsg + "Atenção! A pasta destino deve existir no servidor indicado. (" + svServerName + ")\n";	
			szMsg = szMsg + "(ex: C:\\BaseDados)";	
			szField1 = "Pasta Destino:";
			nResult = SdShowDlgEdit1(szTitle,szMsg,szField1,szDatabasePathNotLocal);
			if(nResult = BACK) goto Dlg_SQLDatabaseName;
			StrRemoveLastSlash(szDatabasePathNotLocal);
    		svDatabasePath = szDatabasePathNotLocal;
	    else
	    	szMsg = "Seleccione a pasta de destino para os ficheiros da Base de Dados do OfficeWorks\n\n";
	    	nResult = AskDestPath(szTitle,szMsg,szDatabasePathLocal,0);
	    	if(nResult = BACK) goto Dlg_SQLDatabaseName;
	    	svDatabasePath = szDatabasePathLocal;
	    endif;
	
	// Validate if datafiles dir is filled
	if(svDatabasePath = "") then
		MessageBox("Não é possível continuar sem indicar a pasta de destino!",WARNING);
		goto Dlg_SQLDatabasePath;        
	endif;      
	
	// Screen to review install options
	Dlg_SdStartCopy2:
	    szTitle = " ";
	    szMsg = "Clique em Instalar para iniciar a instalação da Base de Dados do OfficeWorks.\n\n"; 
	    szMsg = szMsg + "Se pretende rever ou alterar alguma definição, clique em Anterior.\n\nClique em Cancelar para sair do assistente.";
	    nResult = SdStartCopy2(szTitle,szMsg);	
	    if(nResult = BACK) goto Dlg_SQLDatabasePath;
    
    // Set values for runtime replacements
    IFX_PRODUCT_URL = svDatabaseName;
    IFX_PRODUCT_COMMENTS = svDatabasePath;
    return 0;
end;


//---------------------------------------------------------------------------
// OnSQLComponentInstalled
//
// The SQLComponentInstalled event is called after each component is 
// installed so any SQL scripts attached to that component can be run.
//
// szComponent will have the name of the component that has been installed.
//---------------------------------------------------------------------------
function OnSQLComponentInstalled(szComponent)
	string szError,szMsgDialog;
	number nResult,nErrorType,nLine;
begin
    if(SQLRTGetBatchMode()) then
        // Queue up the component to run later
        ISSQLServerAddToBatchInstall(szComponent);
    else
        // Control components instalation
        Disable(BACKBUTTON);
        switch(szComponent)
			// SQL Script to create OfficeWorks Database
			case "CreateDatabase_SQLComponent":
		        nResult = SQLRTComponentInstall(szComponent);
		        if(nResult = SQL_ERROR_ABORT) then
		            // Get error message
				    SQLRTGetComponentScriptError(szComponent,szError,nErrorType,nLine);
		            ShowSQLErrorMessage(szError);
			        szMsgDialog = "A instalação da Base de Dados do OfficeWorks não foi concluido com sucesso!";
			        szMsgDialog = szMsgDialog + "\n\n" + "Não foi possível criar a Base de Dados!";
			        ShowFinishDialog(szMsgDialog);
				endif;
	        // SQL Script to create OfficeWorks Database objects
	        case "CreateDBObjects_SQLComponent":
		        nResult = SQLRTComponentInstall(szComponent);
		        if(nResult = SQL_ERROR_ABORT) then
		            // Get error message
				    SQLRTGetComponentScriptError(szComponent,szError,nErrorType,nLine);
		            ShowSQLErrorMessage(szError);
		            szMsgDialog = "A instalação da Base de Dados do OfficeWorks não foi concluido com sucesso!";
		            szMsgDialog = szMsgDialog + "\n\n" + "Não foi possível criar os objectos da Base de Dados!";
		            ShowFinishDialog(szMsgDialog);
				else
					szMsgDialog = "A instalação da Base de Dados do OfficeWorks foi concluido com sucesso!";
					ShowFinishDialog(szMsgDialog);
				endif;
	        // Ohter SQL Components
	        default:
		        nResult = SQLRTComponentInstall(szComponent);
		        if(nResult = SQL_ERROR_ABORT) then
		            // Get error message
				    SQLRTGetComponentScriptError(szComponent,szError,nErrorType,nLine);
		            ShowSQLErrorMessage(szError);
		            szMsgDialog = "A instalação da Base de Dados do OfficeWorks não foi concluido com sucesso!";
		            szMsgDialog = szMsgDialog + "\n\n" + "Não foi possível instalar os componentes SQL!";
		            ShowFinishDialog(szMsgDialog);
				endif;
		endswitch;
	endif;
end;


//---------------------------------------------------------------------------
// OnCanceling
//
// The OnCanceling event is sent when the installation is cancelled, 
// usually as result of end user clicking Cancel button of a dialog or
// pressing Esc key. Calling Do(EXIT) will also trigger this event.
//---------------------------------------------------------------------------
function OnCanceling()
    STRING szMsgDialog;
begin
    if(IDYES = SprintfBox(MB_YESNO|MB_ICONEXCLAMATION|MB_DEFBUTTON2,SdLoadString(SD_STR_ONCANCELING_TITLE),SdLoadString(SD_STR_ONCANCELING_CONFIRM_MSG))) then
        szMsgDialog = "A instalação da Base de Dados do OfficeWorks foi cancelada pelo utilizador.\n\n\n";
        szMsgDialog = szMsgDialog + "O Sistema não foi modificado.\n\n";
        szMsgDialog = szMsgDialog + "Para instalar a Base de Dados do OfficeWorks mais tarde, execute novamente o assistente.";
        ShowFinishDialog(szMsgDialog);
    endif;
end;

                                         
//---------------------------------------------------------------------------
// GetConnectionInformation
//
// This function will Get connection information
//---------------------------------------------------------------------------
function string GetConnectionInformation()
	LIST    listConn;
	string  szConnection,szDBName;
begin
    listConn = SQLRTGetConnections();
    ListGetFirstString(listConn,szConnection);
    SQLRTGetConnectionInfo(szConnection,svServerName,szDBName,svUserName,svUserPwd);
    ListDestroy(listConn);
end;


//---------------------------------------------------------------------------
// ShowSQLErrorMessage
//
// This function will Show error message to user
// --------------------------------------------------------------------------
function ShowSQLErrorMessage(szErrorMsg)
	string szMessage;
begin
    // Load error string
    szMessage = SdLoadString(IDS_IFX_SQL_ERROR_RUN_FAILED) + "\n\n" + szErrorMsg;
    // Display error
    MessageBox(szMessage, SEVERE);
end;


//---------------------------------------------------------------------------
// ShowFinishDialog
//
// This function will Show Finish Screen
// --------------------------------------------------------------------------
function ShowFinishDialog(szMsgDialog)
	string szTitle,szMsg1,szMsg2,szOpt1,szOpt2;
	BOOL   bvOpt1,bvOpt2;
begin
    // Close the current dialog.
    EndCurrentDialog();
    szTitle = "Instalação da Base de Dados do OfficeWorks";
    szMsg1 = szMsgDialog;    
    if(szMsg1 = "") then
    	szMsg1 = SdLoadString(SD_STR_ONCANCELING_FINISH_MSG1);
    endif;
   	szMsg2 = "Clique em Concluir para terminar o assistente.";    
    szOpt1 = "";
    szOpt2 = "";
    bvOpt1 = FALSE;
    bvOpt2 = FALSE;
    // Display Finish dialog.
    SdFinish(szTitle,szMsg1,szMsg2,szOpt1,szOpt2,bvOpt1,bvOpt2);
    exit;
end;

