//===========================================================================
//
//  File Name:    Setup.rul
//
//  Description:  Blank setup main script file
//
//  Comments:     Blank setup is an empty setup project. If you want to
//				  create a new project via. step-by step instructions use the
//				  Project Assistant.
//
//===========================================================================

// Included header files ----------------------------------------------------
#include "ifx.h"

//User Functions
prototype InitializeRegKeyNameArray();
prototype GetValuesFromRegKeyArray();
prototype SetValuesToRegKeyArray();
prototype BOOL RegKeyExists(STRING);
prototype GetRegistryKeyValues(STRING,NUMBER);
prototype BOOL SetRegistryKeyValues(STRING,NUMBER);
prototype SetRegKeyValue(NUMBER,STRING,STRING,STRING,NUMBER);
prototype VerifyApplicationFeatures();
prototype InstallApplicationFeatures();
prototype BOOL LaunchExternalApplication(STRING,STRING,NUMBER,STRING,NUMBER);
prototype ShowFinishDialog(STRING,BOOL);

//Global Variables
STRING 	svVirtualDirName,svWebServer,svWebService,svTempDirPath;
STRING 	svInstallDirPath;
STRING  svRegKeyName(),svRegKeyValue();
BOOL	bInstallSOAP;

// Global Constants
// OfficeWorks Registry Key
#define kMASTER_REG_KEY					"\\SOFTWARE\\OW"
// Registry Keys Names
#define kVIRTUAL_DIR_NAME_KEY			"VirtualDirectory"
#define kKEY_IDX_00						0
#define kWEB_SERVER_KEY					"WebServer"
#define kKEY_IDX_01						1
#define kWEB_SERVICE_KEY				"WebService"
#define kKEY_IDX_02						2
#define kLOCAL_TEMP_DIR_KEY				"Local_Temp_Dir"
#define kKEY_IDX_03						3
// Total of Registry Keys
#define kTOTAL_REGISTRY_KEYS			4
// Default values
#define kPRODUCT_NAME					"OWScan"
// Product Description
#define kPRODUCT_DESCRIPTION			"OWScan - Módulo de Digitalização de Documentos do OfficeWorks"
// OfficeWorks License file
#define kLICENSE_FILE_NAME				"Licenca_Utilizacao_OWScan.rtf"
// Features names
#define kFEATURES_NAMES					"SOAP Toolkit 3.0"
// Reg Key Name of Features to be Installed
#define kSOAPTOOLKIT_REG_KEY			"\\SOFTWARE\\Microsoft\\MSSOAP\\30"


//---------------------------------------------------------------------------                                                                        
// OnFirstUIBefore
//
// First Install UI Sequence - Before Move Data
// The OnFirstUIBefore event is called by OnShowUI when the setup is
// running in first install mode. By default this event displays UI allowing
// the end user to specify installation parameters.
//
// Note: This event will not be called automatically in a
// program...endprogram style setup.
//---------------------------------------------------------------------------
function OnFirstUIBefore()
    number  nResult;
    string  szTitle,szMsg,szDir;
    string  szField1,szField2,szField3;
    string  szLicenseFile,szOpt1,szOpt2,szMsgDialog;
    BOOL	bLicenseAccepted;
begin
	// Set setup title
	IFX_SETUP_TITLE = "Instalação do " + kPRODUCT_NAME + " - InstallShield Wizard";
	// Beginning of UI Sequence
	Dlg_Start:
	    nResult = 0;
	    //Initialize arrays of keys
	    Resize(svRegKeyName,kTOTAL_REGISTRY_KEYS);
		Resize(svRegKeyValue,kTOTAL_REGISTRY_KEYS);
		InitializeRegKeyNameArray();
		//Get Registry keys values
		GetRegistryKeyValues(kMASTER_REG_KEY,WARNING);
		// Initialize variables
   		svInstallDirPath = FOLDER_APPLICATIONS ^ IFX_COMPANY_NAME ^ kPRODUCT_NAME;
   		bLicenseAccepted = FALSE;
		szLicenseFile = SUPPORTDIR ^ kLICENSE_FILE_NAME;

    // Welcome screen
	Dlg_SdWelcome:
	    szTitle = "Bem-vindo ao assistente de instalação do " + kPRODUCT_NAME + ".";
	    szMsg = "O InstallShield Wizard vai instalar o " + kPRODUCT_DESCRIPTION + ".\n\nPara continuar faça clique sobre Seguinte.";
	    nResult = SdWelcome(szTitle,szMsg);
	    if (nResult = BACK) goto Dlg_Start;
	
	// License Dialog
	Dlg_SdLicense:
	    szTitle = " ";
	    nResult = SdLicense2Rtf(szTitle,szOpt1,szOpt2,szLicenseFile,bLicenseAccepted);
	    if (nResult = BACK) goto Dlg_SdWelcome;
	
 	// Validate Required features
    VerifyApplicationFeatures();
	InstallApplicationFeatures();
	VerifyApplicationFeatures();
	if(bInstallSOAP) then
		MessageBox("Não é possível continuar a instalação sem instalar os requisitos.",WARNING);
		szMsgDialog = "A instalação do " + kPRODUCT_NAME + " não foi concluida.\n\n";
		szMsgDialog = szMsgDialog + "Os requisitos (" + kFEATURES_NAMES + ") para o " + kPRODUCT_NAME + " não foram instalados.";
		// Show finish dialog    
 	   ShowFinishDialog(szMsgDialog,TRUE);
	endif;

 	// Get Virtual dir, Server and database info
	Dlg_VDir_WebServer_WebService_Info:
	    szTitle = " ";
	    szMsg = "Indique o nome da Directoria Virtual, o nome do Servidor Web e o endereço do Serviço Web do OfficeWorks.\n(ex Serviço Web:http://localhost/OWApi/OWApi.asmx)"; 
     	szField1 = "Directoria Virtual:";
     	szField2 = "Servidor Web:";
     	szField3 = "Serviço Web:";
     	nResult = SdShowDlgEdit3(szTitle,szMsg,szField1,szField2,szField3,svVirtualDirName,svWebServer,svWebService);
		if(nResult = BACK) goto Dlg_SdLicense;

	// Validate if virtual dir name is filled
	if(svVirtualDirName = "") then
		MessageBox("Não é possível continuar sem indicar o nome da Directoria Virtual!",WARNING);
		goto Dlg_VDir_WebServer_WebService_Info;        
	endif;       	
	// Validate if web server name is filled
	if(svWebServer = "") then
		MessageBox("Não é possível continuar sem indicar o nome do Servidor Web!",WARNING);
		goto Dlg_VDir_WebServer_WebService_Info;        
	endif;       	
	// Validate if web service name is filled
	if(svWebService = "") then
		MessageBox("Não é possível continuar sem indicar o nome do Serviço Web!",WARNING);
		goto Dlg_VDir_WebServer_WebService_Info;        
	endif;       	
    
 	// Get temp dir info
	Dlg_TemporaryDir_Info:
		szTitle = " ";
	    szMsg = "Seleccione a pasta para a Directoria Temporária do OfficeWorks"; 
	    szDir = svTempDirPath;
     	nResult = AskDestPath(szTitle,szMsg,szDir,0);
     	svTempDirPath = szDir;
		if(nResult = BACK) goto Dlg_VDir_WebServer_WebService_Info;
		
	// Validate if temp dir is filled
	if(svTempDirPath = "") then
		MessageBox("Não é possível continuar sem indicar a Directoria Temporária!",WARNING);
		goto Dlg_TemporaryDir_Info;        
	endif;       	
 	
 	// Get install dir info
	Dlg_InstallDir_Info:
		szTitle = " ";
	    szMsg = "Seleccione a pasta para a instalação do " + kPRODUCT_NAME + "."; 
	    szDir = svInstallDirPath;
     	nResult = AskDestPath(szTitle,szMsg,szDir,0);
     	svInstallDirPath = szDir;
		if(nResult = BACK) goto Dlg_TemporaryDir_Info;

	// Validate if install dir is filled
	if(svInstallDirPath = "") then
		MessageBox("Não é possível continuar sem indicar a Directoria de Instalação!",WARNING);
		goto Dlg_InstallDir_Info;        
	endif;       	

	// Screen to review install options
	Dlg_SdStartCopy2:
	    szTitle = " ";
	    szMsg = "Clique em Instalar para iniciar a instalação do " + kPRODUCT_NAME + ".\n\n"; 
	    szMsg = szMsg + "Se pretende rever ou alterar alguma definição, clique em Anterior.\n\nClique em Cancelar para sair do assistente.";
	    nResult = SdStartCopy2(szTitle,szMsg);	
	    if (nResult = BACK) goto Dlg_InstallDir_Info;
    
   	//Set values to instalation
   	TARGETDIR = svInstallDirPath;
    return 0;
end;


//---------------------------------------------------------------------------
// OnFirstUIAfter
//
// First Install UI Sequence - After Move Data
//
// The OnFirstUIAfter event called by OnShowUI after the file transfer
// of the setup when the setup is running in first install mode. By default
// this event displays UI that informs the end user that the setup has been
// completed successfully.
//
// Note: This event will not be called automatically in a
// program...endprogram style setup.
//---------------------------------------------------------------------------
function OnFirstUIAfter()
	BOOL 	bResult;
    STRING  szMsgDialog,szKeyMaster;
    STRING  szProgram,szFile,szCmdLine,szMsgApp;
begin
  	szMsgDialog = "A instalação do " + kPRODUCT_NAME + " foi concluida.\n\n";
    //Set registry keys
    SdShowMsg("A escrever as chaves do " + kPRODUCT_NAME + " no Registry. Aguarde ...",TRUE);
    szKeyMaster = kMASTER_REG_KEY;
    bResult = SetRegistryKeyValues(szKeyMaster,WARNING);
    if(!bResult) then
    	szMsgDialog = szMsgDialog + "Verifique as chaves no registry para o " + kPRODUCT_NAME + ".\n"; 
    endif;
  	SdShowMsg("",FALSE);
	// Show finish dialog    
    ShowFinishDialog(szMsgDialog,FALSE);
end;


// --------------------------------------------------------------------------
// OnCanceling
//
// The OnCanceling event is sent when the installation is cancelled, 
// usually as result of end user clicking Cancel button of a dialog or
// pressing Esc key. Calling Do(EXIT) will also trigger this event.
// --------------------------------------------------------------------------
function OnCanceling()
    STRING szMsgDialog;
begin
    if(IDYES = SprintfBox(MB_YESNO|MB_ICONEXCLAMATION|MB_DEFBUTTON2,SdLoadString(SD_STR_ONCANCELING_TITLE),SdLoadString(SD_STR_ONCANCELING_CONFIRM_MSG))) then
        szMsgDialog = "A instalação do " + kPRODUCT_NAME + " foi cancelada pelo utilizador.\n\n\n";
        szMsgDialog = szMsgDialog + "O Sistema não foi modificado.\n\n";
        szMsgDialog = szMsgDialog + "Para instalar o " + kPRODUCT_NAME + " mais tarde, execute novamente o assistente.";
        ShowFinishDialog(szMsgDialog,TRUE);
    endif;
end;


// --------------------------------------------------------------------------
// InitializeRegKeyNameArray
//
// This function will fill array of keys name
// --------------------------------------------------------------------------
function InitializeRegKeyNameArray()
begin
    //Virtual Dir
    svRegKeyName(kKEY_IDX_00) = kVIRTUAL_DIR_NAME_KEY;
	//Web Server    
    svRegKeyName(kKEY_IDX_01) = kWEB_SERVER_KEY;
    //Web Service
    svRegKeyName(kKEY_IDX_02) = kWEB_SERVICE_KEY;
	//Temp Dir  
    svRegKeyName(kKEY_IDX_03) = kLOCAL_TEMP_DIR_KEY;
end;


// --------------------------------------------------------------------------
// GetValuesFromRegKeyArray
//
// This function will get values from registry key value array
// --------------------------------------------------------------------------
function GetValuesFromRegKeyArray()
begin
    //Virtual Dir
    svVirtualDirName = svRegKeyValue(kKEY_IDX_00);
    //Web Server
    svWebServer = svRegKeyValue(kKEY_IDX_01);
    //Web Service
    svWebService = svRegKeyValue(kKEY_IDX_02);
    //Temp Dir 
    svTempDirPath = svRegKeyValue(kKEY_IDX_03);
end;


// --------------------------------------------------------------------------
// SetValuesToRegKeyArray
//
// This function will set values to registry key value array
// --------------------------------------------------------------------------
function SetValuesToRegKeyArray()
begin
    //Virtual Dir
    svRegKeyValue(kKEY_IDX_00) = svVirtualDirName;
    //Web Server
    svRegKeyValue(kKEY_IDX_01) = svWebServer;
    //Web Service
    svRegKeyValue(kKEY_IDX_02) = svWebService;
    //Temp Dir 
    svRegKeyValue(kKEY_IDX_03) = svTempDirPath;
end;


// --------------------------------------------------------------------------
// RegKeyExists
//
// This function will verify if registry key exists
// --------------------------------------------------------------------------
function BOOL RegKeyExists(szKeyName) 
begin
    //Set default root key
    RegDBSetDefaultRoot(HKEY_LOCAL_MACHINE);
    //Verify if key exists
	if(RegDBKeyExist(szKeyName) < 0) then
		return FALSE;
	endif;
	return TRUE;
end;


// --------------------------------------------------------------------------
// GetRegistryKeyValues
//
// This function will get registry key values
// --------------------------------------------------------------------------
function GetRegistryKeyValues(szKeyMaster,nMsgType) 
	NUMBER	nResult,nRootKey;                                         
    STRING  szKeyName,szKeyValue;
    NUMBER  n_Type,n_Size;
    INT  	iCount;
begin
    nRootKey = HKEY_LOCAL_MACHINE;
    RegDBSetDefaultRoot(nRootKey);
    //Verify if key exists
	if(RegDBKeyExist(szKeyMaster) < 0) then
		return -1;
	endif;
	n_Type = REGDB_STRING;
   	n_Size = -1;
	// Get keys from registry
	for iCount = 0 to kTOTAL_REGISTRY_KEYS - 1
		szKeyValue = "";    
    	nResult = RegDBGetKeyValueEx(szKeyMaster,svRegKeyName(iCount),n_Type,szKeyValue,n_Size);
    	svRegKeyValue(iCount) = szKeyValue;
	endfor;    
	// Get values from reg key array
	GetValuesFromRegKeyArray();
end;


// --------------------------------------------------------------------------
// SetRegistryKeyValues
//
// This function will set registry keys
// --------------------------------------------------------------------------
function BOOL SetRegistryKeyValues(szKeyMaster,nMsgType)
	NUMBER	nResult,nRootKey;                                         
    STRING  szKeyName,szKeyValue;
    BOOL 	bSetKeysOK;
    INT  	iCount;
begin
    bSetKeysOK = TRUE;
    nRootKey = HKEY_LOCAL_MACHINE;
	// Set values to array for write on registry
    SetValuesToRegKeyArray();
	// Write keys on registry
	for iCount = 0 to kTOTAL_REGISTRY_KEYS - 1    
    	nResult = SetRegKeyValue(nRootKey,szKeyMaster,svRegKeyName(iCount),svRegKeyValue(iCount),nMsgType);
    	if(nResult < 0) then
    		bSetKeysOK = FALSE;
    	endif;
    endfor;
    return bSetKeysOK; 
end;


// --------------------------------------------------------------------------
// SetRegKeyValue
//
// This function will write keys on Registry
// --------------------------------------------------------------------------
function SetRegKeyValue(nRootKey,szKeyMaster,szKeyName,szKeyValue,nMsgType)
	NUMBER n_Type,n_Size;
begin
	RegDBSetDefaultRoot(nRootKey);
	n_Type = REGDB_STRING;
   	n_Size = -1;
    //Write Key
	if(RegDBSetKeyValueEx(szKeyMaster,szKeyName,n_Type,szKeyValue,n_Size) < 0) then
		MessageBox("Não foi possível criar a chave '" + szKeyMaster + "\\" + szKeyName + "' no Registry",nMsgType);
		return -1;
	endif;
	return 0;
end;


// --------------------------------------------------------------------------
// VerifyApplicationFeatures
//
// This function will verify if features are already installed
// --------------------------------------------------------------------------
function VerifyApplicationFeatures()
begin
    SdShowMsg("A verificar os requisitos para a instalação do " + kPRODUCT_NAME + ". Aguarde ...",TRUE);
    //SOAP Toolkit 3.0
   	bInstallSOAP = TRUE;
   	if(RegKeyExists(kSOAPTOOLKIT_REG_KEY)) then
   		bInstallSOAP = FALSE;
   	endif;
   	SdShowMsg("",FALSE); 
end;


// --------------------------------------------------------------------------
// InstallApplicationFeatures
//
// This function will install required features
// --------------------------------------------------------------------------
function InstallApplicationFeatures()
    STRING  szProgram,szFile,szCmdLine,szMsgApp;
begin
  	//Install SOAP Toolkit 3.0
    if(bInstallSOAP) then
	    SdShowMsg("A instalar os requisitos para o " + kPRODUCT_NAME + " (SOAP Toolkit 3.0). Aguarde ...",TRUE);
	    szProgram = "msiexec.exe";
	    szFile = SUPPORTDIR ^ "soapsdk.msi";
	    szCmdLine = " /i \"" + szFile + "\" /qn";
		szMsgApp = "Não foi possível instalar o SOAP Toolkit 3.0 para o " + kPRODUCT_NAME + ".";
		LaunchExternalApplication(szProgram,szCmdLine,LAAW_OPTION_WAIT,szMsgApp,WARNING);
	  	SdShowMsg("",FALSE);
	endif;
end;


//---------------------------------------------------------------------------
// LaunchExternalApplication
//
// This function will run an external application
//---------------------------------------------------------------------------
function BOOL LaunchExternalApplication(szProgram,szCmdLine,nWait,szMsg,nMsgType)
	NUMBER nResult;
begin
    nResult = LaunchAppAndWait(szProgram,szCmdLine,nWait);
	if(nResult < 0) then
		MessageBox(szMsg,nMsgType);
		return FALSE;
	endif;
	return TRUE;
end; 


//---------------------------------------------------------------------------
// ShowFinishDialog
//
// This function will Show Finish Screen and end setup
// --------------------------------------------------------------------------
function ShowFinishDialog(szMsgDialog,bAbort)
	string szMessage,szTitle,szMsg1,szMsg2,szOpt1,szOpt2;
	BOOL   bvOpt1,bvOpt2;
begin
    // Close the current dialog.
    EndCurrentDialog();
    szTitle = "Instalação do " + kPRODUCT_DESCRIPTION;
    szMsg1 = szMsgDialog;    
    if(szMsg1 = "") then
    	szMsg1 = SdLoadString(SD_STR_ONCANCELING_FINISH_MSG1);
    endif;
   	szMsg2 = "Clique em Concluir para terminar o assistente.";    
    szOpt1 = "";
    szOpt2 = "";
    bvOpt1   = FALSE;
    bvOpt2   = FALSE;
    // Display Finish dialog.
    SdFinish(szTitle,szMsg1,szMsg2,szOpt1,szOpt2,bvOpt1,bvOpt2);                       
    if(bAbort) then
    	abort;
    endif;
    exit;
end;

